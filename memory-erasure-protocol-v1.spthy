/* Implementation of the Hancke-Kuhn distance bounding protocol */
/* Demonstrating the DB-secure property */

theory memoryErasure

begin

functions: h/1, mac/1

// -------------------- Tag Initialization -------------------- //

    // Rule for registering a prover/verifier. We create "agent" facts
    // Because the verifier doesn't necessarily know his communication partner
    // When he sends his first message (but we want to track V's session)
rule Register_Keypair:
  [ Fr(~key) ]
--[ KeyGen($A, $V, ~key) ,Once($A, $V), Prelim()]->
  [ !SharedKey($A, $V, ~key), !Agent($A), !Agent($V) ]

// -------------------- Corrupted Agent Rules ---------------- //

    // There's a small difference between a corrupted prover and a corrupted
    // verifier, that comes into play when we make our security claim later.
    // This is a consequence of differences between prover impersonation and
    // verifier impersonation attacks

rule Corrupt_Prover:
  [ !SharedKey($A, $V, key) ]
--[ Corrupted($A, $V), Prelim(), Corrupt($A) ]->
  [ Out(key), !CorruptAgent($A) ]

rule Corrupt_Verifier:
  [ !SharedKey($A, $V, key) ]
--[ Corrupt($V), Prelim() ]->
  [ Out(key) ]

    // The adversary can only interact with the network using the In() and Out() facts:
    // The FastMsg fact is used for messages sent during the fast phase.
    // If the adversary wants to send a message in the fast phase, they have to use a corrupted
    // prover as a "voice"
rule Corrupt_Fast:
  [ !CorruptAgent($A), In(msg) ]
--[ CAction($A), Action($A) ]->
  [ FastMsg(msg) ]

// -------------------- Protocol Rules -------------------- //

  // The Prover P sends the nonce n, as well as their own name

rule Prover_1:
  [ Fr(~n), !Agent($P) ]
--[ ProverOne($P, ~n) ]->
  [ Out(<~n, $P>), ProverStart($P, ~n) ]

  // The verifier receives a message from P, with whom he has a shared key
  // He replies by sending a nonce m of his own

rule Verifier_1:
  [ Fr(~m), In(<n, $P>), !SharedKey($P, $V, key) ]
--[ VerifierOne($P, $V, n, ~m), Action($V) ]->
  [ Out(<mac(<key, n, ~m>), ~m, $V>), VerifierStart($P, $V, n, ~m)]


  // The prover receives the verifier's nonce and prepare for the fast phase

rule Prover_2:
  [ In(<mac(<key, n, m>), m, $V>), !SharedKey($P, $V, key), ProverStart($P, n) ]
--[ ProverTwo($P, $V, n, m), Action($P) ]->
  [ ProverTwo($P, $V, n, m)]

  // The verifier sends a fresh challenge and signal the start of the fast phase

rule Verifier_2:
  [ Fr(~c), !SharedKey($P, $V, key), VerifierStart($P, $V, n, m)]
--[ VerifierTwo($P, $V, n, m, ~c), StartFastPhase($P, $V, n, m, ~c) ]->
  [ FastMsg(~c), Out(~c), VerifierChallenge($P, $V, n, m, ~c) ]


  // The prover responds by sending a hash of all the nonces sent so far
  // as well as the shared key between them and V

rule Prover_3:
  let fastmsg = h(<key, n, m, c>) in
  [ FastMsg(c), ProverTwo($P, $V, n, m), !SharedKey($P, $V, key) ]
--[ ProverThree($P, $V, n, m, c), Action($P)]->
  [ FastMsg(fastmsg), Out(fastmsg) ]

  // The verifier ends the fast phase and checks everything lines up

rule Verifier_3:
  [ FastMsg(h(<key, n, m, c>)), !SharedKey($P, $V, key), VerifierChallenge($P, $V, n, m, c) ]
--[ EndFastPhase($P, $V, n, m, c), VerifierComplete($P, $V, n, m, c) ]->
  []

// ------------------- Axioms -------------------------- //

  // A given pair of agents can only have keypair between them

axiom at_most_once:
    "
    All A V #t1 #t2. Once(A, V)@t1 & Once(A, V)@t2 ==>
        (
        #t1 = #t2
        )
    
    
    "

  // Key initialization rules always take place before protocol rules
  // This restriction is not necessary, but helps tidy up the trace
  // and slightly tightens the security requirement

/*  
axiom prelims_first:
    "
    All A #t1 #t2. Prelim()@t1 & Action(A)@t2 ==>
    (
        t1 < t2
    )

    "    
*/

// -------------------- Sanity Check Lemmas -------------------- //

  // These lemmas check that certain points in the trace are reachable.
  // They're useful to check that the protocol has been correctly implemented
  // but are not necessary for the analysis

lemma prover1_exists:
    exists-trace
  "
    Ex P n #t. (
        ProverOne( P, n )@t )
  "

lemma verifier1_exists:
    exists-trace
  "
    Ex P V n m #t. (
        VerifierOne(P, V, n, m)@t )

  "

lemma prover2_exists:
    exists-trace
  "
    Ex P V m n #t. (
        ProverTwo(P, V, m, n)@t )

  "

lemma verif2_exists:
    exists-trace
  "
    Ex P V m n c #t. (
        VerifierTwo(P, V, m, n, c)@t )

  "

lemma prover3_exists:
    exists-trace
  "
    Ex P V m n c #t. (
        ProverThree(P, V, m, n, c)@t )

  "

lemma finish_exists:
    exists-trace
  "
   ( Ex P V m n c #t. (
        VerifierComplete(P, V, m, n, c)@t )
   ) &
	not( Ex A B #t1. Corrupted(A, B)@t1 )
  "

// -------------------- Security Lemmas -------------------- //

lemma dbsec:
  "
  All P V m n c #t. (
    VerifierComplete(P, V, m, n, c)@t ) ==>                   // Whenever the verifier is complete EITHER:
    (Ex #tc. Corrupt(V)@tc)|                                  //  1) The verifier is corrupt OR
    (                                                         
      Ex #t1 #t2 #t3. StartFastPhase(P, V, m, n, c)@t1 &              //  2) There exists t1, t2 and t3 such that
                  Action(P)@t2 &                              //          The fast phase was between t1 and t3
                  EndFastPhase(P, V, m, n, c)@t3 &            //          and the prover did something between those two times
                  (#t1 < #t2) &                               //          and the verifier completed at or after the
                  (#t2 < #t3) &                               //          end of the fast phase OR
                  ( (#t3 < #t ) | (#t3 = #t) )
    )|
    (              
      Ex CAgent #t4 #t5 #t6 #t7. StartFastPhase(P, V, m, n, c)@t5 &    //  3) There exist times t5, t6 and t7 such that
                             EndFastPhase(P, V, m, n, c)@t7 & //        The fast phase was between t5 and t7
                             Corrupted(P, V)@t4 &             //        and the prover P is dishonest
                             CAction(CAgent)@t6 &             //        and some dishonest agent did something during
                             (#t5 < #t6)&                     //        the fast phase
                             (#t6 < #t7)&                     //        and the verifier completed at or after the
                             ( (#t7 < #t) | (#t7 = #t) )      //        end of the fast phase 
    ) 
  "

lemma secMemErasure:
  "
  All P V m n c #t. (
    VerifierComplete(P, V, m, n, c)@t ) ==>                   // Whenever the verifier is complete EITHER:
    (Ex #tc. Corrupt(V)@tc)|                                  //  1) The verifier is corrupt OR
    (                                                         
      Ex #t1 #t2 #t3. StartFastPhase(P, V, m, n, c)@t1 &              //  2) There exists t1, t2 and t3 such that
                  ProverThree(P, V, m, n, c)@t2 &                              //          The fast phase was between t1 and t3
                  //Action(P)@t2 &                              //          The fast phase was between t1 and t3
                  EndFastPhase(P, V, m, n, c)@t3 &            //          and the prover did something between those two times
                  (#t1 < #t2) &                               //          and the verifier completed at or after the
                  (#t2 < #t3) &                               //          end of the fast phase OR
                  ( (#t3 < #t ) | (#t3 = #t) )
    )|
    (              
      Ex CAgent #t4 #t5 #t6 #t7. StartFastPhase(P, V, m, n, c)@t5 &    //  3) There exist times t5, t6 and t7 such that
                             EndFastPhase(P, V, m, n, c)@t7 & //        The fast phase was between t5 and t7
                             Corrupt(P)@t4 &             //        and the prover P is dishonest
                             (#t4 < #t7)&                     //        the fast phase
                             CAction(CAgent)@t6 &             //        and some dishonest agent did something during
                             (#t5 < #t6)&                     //        the fast phase
                             (#t6 < #t7)&                     //        and the verifier completed at or after the
                             ( (#t7 < #t) | (#t7 = #t) )      //        end of the fast phase 
    ) 
  "



lemma Client_auth:
  " /* For all session with a verifier */
    ( All P V n m #i.  ProverTwo(P, V, n, m) @ #i
       ==>
         /* there is a verifier that answered the request */
       ( 
		(Ex #a. VerifierOne(P, V, n, m) @ a)
		 /* or the adversary performed a long-term key reveal on 'V' or 'P'
		    before the key was setup. */
		| (Ex #r. Corrupt(V) @ r & r < i)
		| (Ex #r. Corrupted(P, V) @ r & r < i) 
       )
    )
  "

end

